plugins {
    id 'java'
}

allprojects {
    apply plugin: 'java'

    group = 'net.gudenau.cavegame'
    version = '0.0.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:${project.annotationsVersion}"

        if(project.name != 'logger') {
            implementation project(':logger')

            if(project.name != 'utilities' && project.name != 'launcher') {
                implementation project(':utilities')
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of("${project.javaVersion}")
        }

        modularity.inferModulePath = true
    }

    apply from: rootProject.projectDir.toPath().resolve(java.nio.file.Path.of('scripts', 'lwjgl.gradle')).toAbsolutePath().toString()
}

dependencies {
    implementation "it.unimi.dsi:fastutil:${project.fastutilVersion}"
    implementation "com.google.code.gson:gson:${project.gsonVersion}"
    implementation "net.jodah:typetools:${project.typetoolsVersion}"

    implementation "org.joml:joml:${jomlVersion}"

    implementation 'org.lwjgl:lwjgl'
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"

    implementation 'org.lwjgl:lwjgl-jemalloc'
    runtimeOnly "org.lwjgl:lwjgl-jemalloc::$lwjglNatives"

    implementation project(':wooting')
    implementation project(':renderer')
    implementation project(':vulkan')
}

tasks.register('runGame', JavaExec) {
    dependsOn jar

    group = 'Execution'
    description = 'Runs the game'
    classpath = files(jar.archiveFile, sourceSets.main.runtimeClasspath) - sourceSets.main.output
    mainModule = 'net.gudenau.cavegame'
    mainClass = 'net.gudenau.cavegame.CaveGame'

    args = ['-debug', 'true', '-renderer', 'CaveGameVk']
    jvmArguments = [
        '--add-modules', 'ALL-MODULE-PATH'
    ]
    /*
    environment += [
        'MANGOHUD': '1'
    ]
     */

    javaLauncher = javaToolchains.launcherFor(java.toolchain)
}
